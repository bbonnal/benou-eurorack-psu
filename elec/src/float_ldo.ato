import Power from "generics/interfaces.ato"
import VDiv from "generics/vdivs.ato"
import Resistor from "generics/resistors.ato"
import Capacitor from "generics/capacitors.ato"
import FloatPower from "generics/interfaces.ato"
import PWR263S_35_R500J from "PWR263S_35_R500J.ato"
import ES3J_C241736 from "ES3J_C241736.ato"


module _LDOFloat:

    power_in = new FloatPower
    power_out = new FloatPower

    power_in.v_neg ~ power_out.v_neg

    r_in_shunt1 = new PWR263S_35_R500J
    r_in_shunt2 = new PWR263S_35_R500J

    r_in_shunt1.value = 0.5R +/- 5%
    r_in_shunt2.value = 0.5R +/- 5%

    r_set = new Resistor
    r_set.footprint = "R0805"
    r_set.mpn = "C865322"

    c_set = new Capacitor
    c_set.footprint = "C0805"
    c_set.value = 10pF +/- 30%


    # input cap
    input_bypass = new Capacitor
    input_bypass.value = 22uF +/- 20%
    input_bypass.footprint = "C1206"
    input_bypass.mpn = "C5162260"

    input_bypass.p1 ~ power_in.v_pos
    input_bypass.p2 ~ power_in.v_neg

    # output cap
    output_bypass = new Capacitor
    output_bypass.value = 22uF +/- 20%
    output_bypass.footprint = "C1206"
    output_bypass.mpn = "C5162260"
    output_bypass.p1 ~ power_out.v_pos
    output_bypass.p2 ~ power_out.v_neg


    ldo = new _LT3083EQ_PBF

    #diodes to prevent bacward currents throught the ldo due to floating during startup
    d_protect_pos = new ES3J_C241736
    d_protect_neg = new ES3J_C241736

    #power input through shunts
    power_in.v_pos ~ r_in_shunt1.p1
    r_in_shunt1.p2 ~ r_in_shunt2.p1
    r_in_shunt2.p2 ~ ldo.IN

    #direct power input for internal current source
    power_in.v_pos ~ ldo.VCONTROL


    #voltage output setting
    ldo.SET ~ r_set.p1
    ldo.SET ~ c_set.p1
    r_set.p2 ~ power_in.v_neg
    c_set.p2 ~ power_in.v_neg

    #anti backwards current diodes
    d_protect_pos.CATODE ~ldo.IN
    d_protect_pos.ANODE ~ ldo.OUT

    d_protect_neg.CATODE ~ ldo.OUT
    d_protect_neg.ANODE ~ power_out.v_neg

    ldo.OUT ~ power_out.v_pos


module LDOFloat5V from _LDOFloat:

    r_set.value = 100kohm +/- 5%
    r_set.mpn = "C122537"

module LDOFloat12V from _LDOFloat:

    r_set.value =243kohm +/- 5%
    r_set.mpn = "C865322"

component _LT3083EQ_PBF:
    # component LT3083EQ#PBF
    footprint = "DD-PAK-5_L10.2-W8.9-P1.70-LS14.0-BR"
    lcsc_id = "C459910"
    mpn = "C459910"
    # pins
    signal NC ~ pin 1
    signal SET ~ pin 2
    signal OUT ~ pin 3
    signal VCONTROL ~ pin 4
    signal IN ~ pin 5
    OUT ~ pin 6